#pragma version 6

txn OnCompletion
int NoOp
==
bnz handle_noop

txn OnCompletion
int OptIn
==
bnz handle_optin

txn OnCompletion
int CloseOut
==
bnz handle_closeout

txn OnCompletion
int UpdateApplication
==
bnz handle_updateapp

txn OnCompletion
int DeleteApplication
==
bnz handle_deleteapp

// Unexpected OnCompletion value. Should be unreachable.
b decline
////////////////

////////////////
handle_noop:

txn ApplicationArgs 0
byte "optin"
==
bnz handle_noop_optin

txn ApplicationArgs 0
byte "create"
==
bnz handle_create

// Unexpected first arg
b decline
////////////////

////////////////
handle_noop_optin:

// checks
int pay
gtxn 0 TypeEnum
==
gtxn 0 Amount
int 1000000
==
gtxn 0 Receiver
global CurrentApplicationAddress
==
gtxna 3 ApplicationArgs 0
byte "create"
==
global GroupSize
int 4
==
txn GroupIndex
int 1
==
&&
&&
&&
&&
&&
assert

// opt-into ASA
itxn_begin
int axfer
itxn_field TypeEnum
global CurrentApplicationAddress
itxn_field AssetReceiver
txna Assets 0
itxn_field XferAsset
int 0
itxn_field AssetAmount
itxn_submit

b approve
////////////////

////////////////
handle_create:

// checks
int pay
gtxn 0 TypeEnum
==
gtxn 0 Amount
int 1000000
==
gtxn 0 Receiver
global CurrentApplicationAddress
==
gtxna 1 ApplicationArgs 0
byte "optin"
==
global GroupSize
int 4
==
txn GroupIndex
int 3
==
&&
&&
&&
&&
&&
assert

// create new dapp
itxn_begin
int appl
itxn_field TypeEnum
byte base64 BiAFAAQBAqCNBiYECUNBTkNBTkNFTAVTVEFSVANFTkQLQkVORUZJQ0lBUlkxGSISQAAgMRkkEkABwzEZJRJAAbkxGSMSQAGkMRmBBRJAAapCAa02GgCABGluaXQSQAAyNhoAgAh3aXRoZHJhdxJAAHs2GgCABnVwZGF0ZRJAANE2GgCABmNhbmNlbBJAATBCAW6xI7IQMgqyFDcAMACyESKyErMiKGVAAVZIMwAAMgkSRDIEJRJEMwEQIxIzARE3ADAAEhBENwAaAhcyBw1BAS8oNwAaARdnKTIHZyo3ABoCF2crNwAcAWdCARYrZDUAKWQ1CipkNQsxADQAEkSIACA1ATQBQQD4sSOyEDQAshQ2MACyETQBshKzKTIHZ0IA4TIHNAsMQQAdMgc0CgkhBAs0CzQKCQoyCjYwAHAAQQC/CyEECokyCjYwAHAAQQCwiTIJMQASRCIoZURIMgQlEjEWIhIQRDMBECMSMwERNjAAEhBEMgo2MABwAEEAgjUBNhoBFzUCKmQ1AzMBEjUEKWQ1BTQBNAMLNAE0AgsJNAQ0AwsINAQ0BQkiD0Q2GgEXMgcNQQBKKjYaARdnQgBDMgQkEjIJMQASEEQoZEEAMbEjshAyCbIUNjAAshEyCjYwAHAAQQAashKzQgAWMgQkEjIJMQASEERCAAhCAANCAAIiQyRD
itxn_field ApprovalProgram
byte base64 BoEB
itxn_field ClearStateProgram
int 3
itxn_field GlobalNumUint
int 1
itxn_field GlobalNumByteSlice
int 0
itxn_field LocalNumUint
int 0
itxn_field LocalNumByteSlice
int 1
itxn_field OnCompletion
txna ApplicationArgs 4
itxn_field Note
itxn_submit

// new dApp addr
itxn CreatedApplicationID
store 0
load 0
app_params_get AppAddress
bz decline
store 1

// send dapp algo
itxn_begin
int pay
itxn_field TypeEnum
load 1
itxn_field Receiver
int 201000
itxn_field Amount
itxn_submit

// init new dapp
itxn_begin
int appl
itxn_field TypeEnum
load 0
itxn_field ApplicationID
byte "init"
itxn_field ApplicationArgs
txna ApplicationArgs 1
itxn_field ApplicationArgs
txna ApplicationArgs 2
itxn_field ApplicationArgs
txna Accounts 1
itxn_field Accounts
txna Assets 0
itxn_field Assets
txna ApplicationArgs 3
itxn_field Note

// send dapp asa
itxn_next
int axfer
itxn_field TypeEnum
load 1
itxn_field AssetReceiver
txna Assets 0
itxn_field XferAsset
gtxn 2 AssetAmount
itxn_field AssetAmount
itxn_submit

b approve

////////////////


///////////////
// Update only by creator - for now
// TODO maybe make updating impossible for safety and assurance
handle_updateapp:

global GroupSize
int 1
==
global CreatorAddress
txn Sender
==
&&
bnz approve
b decline
///////////////


///////////////
// handle closeOut, cannot delete app, no decline
handle_deleteapp:
b decline
///////////////

///////////////
// handle opt-in
handle_closeout:
handle_optin:
b approve
///////////////

////////////////
// end branches
decline:
int 0
return

approve:
int 1
return
////////////////


////////////////
// fyi
// Txn.Accounts[0] is sender of txn
// Txn.Apps[0] is own app
////////////////